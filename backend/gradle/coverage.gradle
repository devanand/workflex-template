jacoco {
    toolVersion = "0.8.14"
}

/*
 * UNIT TEST COVERAGE
 */
jacocoTestReport {
    dependsOn test

    executionData(fileTree(layout.buildDirectory).include(
            "jacoco/test.exec"
    ))

    sourceDirectories.setFrom files(sourceSets.main.allSource.srcDirs)
    classDirectories.setFrom files(sourceSets.main.output)

    reports {
        xml.required = true
        html.required = true
    }
}

/*
 * INTEGRATION TEST COVERAGE
 */
tasks.register("jacocoIntegrationTestReport", JacocoReport) {
    dependsOn integrationTest

    executionData(fileTree(layout.buildDirectory).include(
            "jacoco/integrationTest.exec"
    ))

    sourceDirectories.setFrom files(sourceSets.main.allSource.srcDirs)
    classDirectories.setFrom files(sourceSets.main.output)

    reports {
        xml.required = true
        html.required = true
    }
}

/*
 * AGGREGATED COVERAGE (Unit + Integration)
 */
tasks.register("jacocoAllReport", JacocoReport) {
    dependsOn test, integrationTest

    executionData(fileTree(layout.buildDirectory).include(
            "jacoco/test.exec",
            "jacoco/integrationTest.exec"
    ))

    sourceDirectories.setFrom files(sourceSets.main.allSource.srcDirs)
    classDirectories.setFrom files(sourceSets.main.output)

    reports {
        xml.required = true
        html.required = true
    }
}